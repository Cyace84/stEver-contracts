pragma ever-solidity >=0.62.0;
pragma AbiHeader expire;

import "./interfaces/IStEverAccount.tsol";
import "./interfaces/IStEverVault.tsol";
import "./utils/ErrorCodes.tsol";
import "./utils/Gas.tsol";
import "./utils/Constants.tsol";

import "@broxus/contracts/contracts/libraries/MsgFlag.tsol";




contract StEverAccount is IStEverAccount {
    address vault; // setup from initData
    address user; // setup from initData
    uint32 currentVersion; //setup from _init

    // mappings
    mapping(uint64 => IStEverAccount.WithdrawRequest) public withdrawRequests;

    constructor() public {
        revert();
    }
    /// @dev this function is using instead of constructor
    /// @param _version - initial version of account
    function _init(uint32 _version) internal {
        currentVersion = _version;
    }

    modifier onlyVault() {
        require (msg.sender == vault, ErrorCodes.ONLY_VAULT);
        _;
    }

    function _reserve() internal pure returns (uint128) {
		return
			math.max(address(this).balance - msg.value, StEverAccountGas.CONTRACT_MIN_BALANCE);
	}

    function getDetails()
		external
		view
		responsible
		override
		returns (IStEverAccount.AccountDetails)
	{
		return
			{
				value: 0,
				bounce: false,
				flag: MsgFlag.REMAINING_GAS
			} AccountDetails(user, vault, currentVersion);
	}

    /// @dev function for handling withdraw requests
    /// @param _nonce - nonce of withdraw request
    /// @param _amount - amount of withdraw request
    /// @param _unlockTime - unlock time of withdraw request
    /// @param _remainingGasTo - address for sending remaining gas
    function addPendingValue(
        uint64 _nonce,
        uint128 _amount,
        uint64 _unlockTime,
        address _remainingGasTo
    ) override external onlyVault {
        tvm.rawReserve(_reserve(), 0);
        if (withdrawRequests.keys().length < Constants.MAX_PENDING_COUNT && !withdrawRequests.exists(_nonce)) {

            withdrawRequests[_nonce] = WithdrawRequest({
                amount: _amount,
                timestamp: now,
                unlockTime: _unlockTime
            });

            IStEverVault(vault).onPendingWithdrawAccepted{value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false}(_nonce, user, _unlockTime, _remainingGasTo);
            return;
        }
        IStEverVault(vault).onPendingWithdrawRejected{value: 0, flag:MsgFlag.ALL_NOT_RESERVED, bounce: false}(_nonce, user, _amount, _remainingGasTo);
    }
    /// @dev function for reset pending withdraw requests in case of vault reject
    /// @param rejectedWithdrawals - mapping of rejected withdraw requests
    /// @param _sendGasTo - address for sending remaining gas
    function resetPendingValues(mapping(uint64 => IStEverAccount.WithdrawRequest) rejectedWithdrawals, address _sendGasTo) override external onlyVault {
        tvm.rawReserve(_reserve(), 0);

        for ((uint64 nonce, IStEverAccount.WithdrawRequest rejectedWithdrawRequest) : rejectedWithdrawals) {
            withdrawRequests[nonce] = rejectedWithdrawRequest;
        }

        _sendGasTo.transfer({value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false});
    }
    /// @dev function for remove pending withdraw request
    /// @param _nonce - nonce of withdraw request
    function removePendingWithdraw(uint64 _nonce) override external onlyVault {
        tvm.rawReserve(_reserve(), 0);
        if (withdrawRequests.exists(_nonce)) {
            IStEverAccount.WithdrawRequest withdrawRequest = withdrawRequests[_nonce];
            delete withdrawRequests[_nonce];
            IStEverVault(vault).onPendingWithdrawRemoved{
                value: 0,
                flag:MsgFlag.ALL_NOT_RESERVED,
                bounce: false
            }(user, _nonce, withdrawRequest.amount);
            return;
        }
        user.transfer({value:0, flag:MsgFlag.ALL_NOT_RESERVED, bounce: false});
    }
    /// @dev function for preparing data about withdraw requests that will send to the vault
    function createAndSendWithdrawToUserRequest(uint64[] _satisfiedWithdrawRequests) internal {

        uint128 totalAmount = 0;
        mapping(uint64 => IStEverAccount.WithdrawRequest) withdrawals;

        for (uint256 i = 0; i < _satisfiedWithdrawRequests.length; i++) {
            uint64 withdrawRequestKey = _satisfiedWithdrawRequests[i];
            if (withdrawRequests.exists(withdrawRequestKey)) {
                IStEverAccount.WithdrawRequest withdrawRequest = withdrawRequests[withdrawRequestKey];
                withdrawals[withdrawRequestKey] = withdrawRequest;
                delete withdrawRequests[withdrawRequestKey];
                totalAmount += withdrawRequest.amount;
            }
        }

        IStEverVault(vault).withdrawToUser{
            value: 0,
            flag: MsgFlag.ALL_NOT_RESERVED,
            bounce: false
        }(
            totalAmount, user, withdrawals
        );
    }

    /// @dev function for processing withdraw requests
    /// @param _satisfiedWithdrawRequests - array of satisfied withdraw requests
    function processWithdraw(uint64[] _satisfiedWithdrawRequests) override external onlyVault {
        tvm.rawReserve(_reserve(), 0);
        createAndSendWithdrawToUserRequest(_satisfiedWithdrawRequests);
    }
    /// @dev function for processing withdraw requests in case of emergency
    function onEmergencyWithdrawToUser() override external onlyVault {
        tvm.rawReserve(_reserve(), 0);
        uint64[] satisfiedWithdrawRequests;
        for((uint64 nonce,) : withdrawRequests) {
            satisfiedWithdrawRequests.push(nonce);
        }
        if (satisfiedWithdrawRequests.length == 0) {
            user.transfer({value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false});
            return;
        }
        createAndSendWithdrawToUserRequest(satisfiedWithdrawRequests);
    }
    /// @dev function for checking if provided withdraw request has been living more than `timeAfterEmergencyCanBeActivated`
    /// @param _proofNonce - nonce of withdraw request
    function onStartEmergency(uint64 _proofNonce) override external onlyVault {
        tvm.rawReserve(_reserve(), 0);
        if (!withdrawRequests.exists(_proofNonce)) {
            IStEverVault(vault).startEmergencyRejected{value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false}(user, ErrorCodes.REQUEST_NOT_EXISTS);
            return;
        }

        IStEverAccount.WithdrawRequest withdrawRequest = withdrawRequests[_proofNonce];
        if ((withdrawRequest.timestamp + Constants.TIME_AFTER_EMERGENCY_CAN_BE_ACTIVATED) > now) {
            IStEverVault(vault).startEmergencyRejected{value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false}(user, ErrorCodes.EMERGENCY_CANT_BE_ACTIVATED);
            return;
        }

        IStEverVault(vault).emergencyWithdrawFromStrategiesProcess{value: 0, flag: MsgFlag.ALL_NOT_RESERVED, bounce: false}(user);
    }

    function upgrade(TvmCell _newCode, uint32 _newVersion, address _sendGasTo) external virtual override onlyVault {


        if (_newVersion == currentVersion) {
            tvm.rawReserve(_reserve(), 0);
            _sendGasTo.transfer({ value: 0, bounce: false, flag: MsgFlag.ALL_NOT_RESERVED });
            return;
        }


        TvmBuilder mainBuilder;
        mainBuilder.store(vault);
        mainBuilder.store(uint8(0));
        mainBuilder.store(_sendGasTo);

        TvmCell dummyPlatformCode;
        mainBuilder.store(dummyPlatformCode);

        TvmCell initialData = abi.encode(user);

        TvmCell constructorParams = abi.encode(_newVersion, currentVersion);

        mainBuilder.storeRef(initialData);
        mainBuilder.storeRef(constructorParams);

        TvmCell storageData = abi.encode(
            vault,              //address
            user,               //address
            currentVersion,     //uint32
            withdrawRequests    //mapping(uint64 => WithdrawRequest)
        );

        mainBuilder.storeRef(storageData);


        // set code after complete this method
        tvm.setcode(_newCode);
        // run onCodeUpgrade from new code
        tvm.setCurrentCode(_newCode);

        onCodeUpgrade(mainBuilder.toCell());
    }

    function onCodeUpgrade(TvmCell _upgradeData) private {
        tvm.resetStorage();
        tvm.rawReserve(_reserve(), 0);
        TvmSlice s = _upgradeData.toSlice();

        (address root_, , address sendGasTo, ) = s.decode(address, uint8, address,TvmCell);
        vault = root_;

        TvmCell initialData = s.loadRef();
        (user) = abi.decode(initialData, (address));

        TvmCell constructorParams = s.loadRef();
        (uint32 _newVersion, uint32 _currentVersion) = abi.decode(constructorParams, (uint32, uint32));

        if (_newVersion == _currentVersion) {
            _init(_newVersion);
        }

        if  (_newVersion != _currentVersion) {
            (
                ,
                ,
                ,
                mapping(uint64 => IStEverAccount.WithdrawRequest) oldWithdrawRequests
            ) = abi.decode(
                s.loadRef(),
                (
                    address,
                    address,
                    uint32,
                    mapping(uint64 => IStEverAccount.WithdrawRequest)
                )
            );
            currentVersion = _newVersion;
            for ((uint64 nonce, IStEverAccount.WithdrawRequest oldWithdrawRequest) : oldWithdrawRequests) {
                withdrawRequests[nonce] = IStEverAccount.WithdrawRequest(oldWithdrawRequest.amount, oldWithdrawRequest.timestamp, 0);
            }
        }


        sendGasTo.transfer({value: 0, bounce: false, flag: MsgFlag.ALL_NOT_RESERVED});
    }
}
